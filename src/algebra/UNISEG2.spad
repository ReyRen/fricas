)abbrev package UNISEG2 UniversalSegmentFunctions2
++ Author:
++ Basic Operations:
++ Related Domains: Segment, UniversalSegment
++ Also See:
++ AMS Classifications:
++ Keywords: equation
++ Examples:
++ References:
++ Description:
++   This package provides operations for mapping functions onto segments.

UniversalSegmentFunctions2(R : Type, S : Type) : with
    map : (R -> S, UniversalSegment R) -> UniversalSegment S
        ++ map(f, seg) returns the new segment obtained by applying
        ++ f to the endpoints of seg.

    if R has OrderedRing then
      map : (R -> S, UniversalSegment R) -> Stream S
        ++ map(f, s) expands the segment s, applying \spad{f} to each value.


  == add
    map(f : R -> S, u : UniversalSegment R) : UniversalSegment S ==
      s := f(low(u))
      hasHi u => segment(s, f(high(u)))
      segment s

    if R has OrderedRing then
      map(f : R -> S, u : UniversalSegment R) : Stream S ==
        map(f, expand u)$StreamFunctions2(R, S)

