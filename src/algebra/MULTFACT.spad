)abbrev package MULTFACT MultivariateFactorize
++ Author: P. Gianni
++ Date Created: 1983
++ Basic Functions:
++ Related Constructors: MultFiniteFactorize, AlgebraicMultFact, UnivariateFactorize
++ Also See:
++ AMS Classifications:
++ Keywords:
++ References:
++ Description:
++   This is the top level package for doing multivariate factorization
++ over basic domains like \spadtype{Integer} or \spadtype{Fraction Integer}.

MultivariateFactorize(OV, E, R, P) : C == T
 where
  R          :   Join(PolynomialFactorizationExplicit, CharacteristicZero)
                    -- with factor on R[x]
  OV         :   OrderedSet
  E          :   OrderedAbelianMonoidSup
  P          :   PolynomialCategory(R, E, OV)
  USP          ==> SparseUnivariatePolynomial P

  C == with
    factor      :      P  ->  Factored P
      ++ factor(p) factors the multivariate polynomial p over its coefficient
      ++ domain
    factor      :      USP  ->  Factored USP
      ++ factor(p) factors the multivariate polynomial p over its coefficient
      ++ domain where p is represented as a univariate polynomial with
      ++ multivariate coefficients
  T == add
    if R has EuclideanDomain then
        if R is Fraction Integer then
            factor(p : P) : Factored P ==
                factor(p)$MRationalFactorize(E, OV, Integer, P)
        else if R is Fraction Complex Integer then
            factor(p : P) : Factored P ==
                factor(p)$MRationalFactorize(E, OV, Complex Integer, P)
        else if R is Fraction Polynomial Integer and
                OV has convert : % -> Symbol then
            factor(p : P) : Factored P ==
                factor(p)$MPolyCatRationalFunctionFactorizer(E, OV, Integer, P)
        else
            factor(p : P) : Factored P ==
                factor(p, factorPolynomial$R)$InnerMultFact(OV, E, R, P)
    else
        factor(p : P) : Factored P == factor(p)$P

    factor(up : USP) : Factored USP ==
        factor(up, factorPolynomial$R)$InnerMultFact(OV, E, R, P)

